#version 450 core
#pragma use_vulkan_memory_model
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_buffer_reference : enable
#extension GL_KHR_shader_subgroup_ballot : enable
#extension GL_EXT_control_flow_attributes : enable

layout(binding=0) buffer InputA { float x[]; } inputA;
layout(binding=1) buffer InputB { vec4 x[]; } inputB;
layout(binding=2) buffer InputC { vec4 x[]; } inputC;
layout(binding=3) buffer Output { vec4 x[]; } outputO;
layout(local_size_x = 16, local_size_y = 4, local_size_z = 1) in;

layout(constant_id = 6) const uint K = 1;
layout(constant_id = 7) const uint strideA = 1;
layout(constant_id = 8) const uint strideB = 1;
layout(constant_id = 9) const uint strideC = 1;
layout(constant_id = 10)const uint strideD = 1;

const uint C_ROWS = TILE_M / 4;
const uint C_COLS = TILE_N / 64;

uint coordToOffset(uint i, uint j, uint stride)
{
    return (stride * i + j);
}

void main()
{
    uvec2 tileID = uvec2(gl_GlobalInvocationID.xy);
    vec4 C[C_ROWS][C_COLS];
    float A[C_ROWS];

    // Initialize result to zero
    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            C[i][j] = vec4(0.f, 0.f, 0.f, 0.f);
        }
    }

    for (uint k = 0; k < K; k++) {
        [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
            uint gi = tileID.y*C_ROWS+i;
            uint gk = k;
            A[i] = inputA.x[coordToOffset(gi, gk, strideA)];
        }
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
          uint gj = tileID.x*C_COLS+j;
          uint gk = k;
          vec4 B = inputB.x[coordToOffset(gk, gj, strideB/4)];
          [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
            C[i][j] += vec4(A[i], A[i], A[i], A[i])*B;
          }
        }
    }

    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            uint gi = tileID.y*C_ROWS+i;
            uint gj = tileID.x*C_COLS+j;
            outputO.x[gi * strideD/4 + gj] = C[i][j];
        }
    }
}
