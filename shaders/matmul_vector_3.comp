#version 450 core
#pragma use_vulkan_memory_model
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_buffer_reference : enable
#extension GL_KHR_shader_subgroup_ballot : enable
#extension GL_EXT_control_flow_attributes : enable

layout(binding=0) buffer InputA { vec4 x[]; } inputA;
layout(binding=1) buffer InputB { float x[]; } inputB;
layout(binding=2) buffer InputC { vec4 x[]; } inputC;
layout(binding=3) buffer Output { float x[]; } outputO;
layout(local_size_x = 16, local_size_y = 1, local_size_z = 1) in;

// M/N/K values filled out at pipeline creation time
layout(constant_id = 0) const uint lM = 1;
layout(constant_id = 1) const uint lN = 1;
layout(constant_id = 2) const uint lK = 1;
layout(constant_id = 3) const uint TILE_M = 1;
layout(constant_id = 4) const uint TILE_N = 1;
layout(constant_id = 5) const uint TILE_K = 1;
layout(constant_id = 6) const uint K = 1;
layout(constant_id = 7) const uint strideA = 1;
layout(constant_id = 8) const uint strideB = 1;
layout(constant_id = 9) const uint strideC = 1;
layout(constant_id = 10)const uint strideD = 1;

const uint C_ROWS = TILE_M / lM;
const uint C_COLS = TILE_N / lN;

uint coordToOffset(uint i, uint j, uint stride)
{
    return (stride * i + j);
}

vec4 C[C_ROWS][C_COLS];

void matmul4x16x16(inout vec4 C, vec4 A, vec4 B0, vec4 B1, vec4 B2, vec4 B3) {
  C.x += B0.x * subgroupBroadcast(A.x, 0);
  C.x += B0.y * subgroupBroadcast(A.y, 0);
  C.x += B0.z * subgroupBroadcast(A.z, 0);
  C.x += B0.w * subgroupBroadcast(A.w, 0);
  C.x += B1.x * subgroupBroadcast(A.x, 1);
  C.x += B1.y * subgroupBroadcast(A.y, 1);
  C.x += B1.z * subgroupBroadcast(A.z, 1);
  C.x += B1.w * subgroupBroadcast(A.w, 1);
  C.x += B2.x * subgroupBroadcast(A.x, 2);
  C.x += B2.y * subgroupBroadcast(A.y, 2);
  C.x += B2.z * subgroupBroadcast(A.z, 2);
  C.x += B2.w * subgroupBroadcast(A.w, 2);
  C.x += B3.x * subgroupBroadcast(A.x, 3);
  C.x += B3.y * subgroupBroadcast(A.y, 3);
  C.x += B3.z * subgroupBroadcast(A.z, 3);
  C.x += B3.w * subgroupBroadcast(A.w, 3);

  C.y += B0.x * subgroupBroadcast(A.x, 4);
  C.y += B0.y * subgroupBroadcast(A.y, 4);
  C.y += B0.z * subgroupBroadcast(A.z, 4);
  C.y += B0.w * subgroupBroadcast(A.w, 4);
  C.y += B1.x * subgroupBroadcast(A.x, 5);
  C.y += B1.y * subgroupBroadcast(A.y, 5);
  C.y += B1.z * subgroupBroadcast(A.z, 5);
  C.y += B1.w * subgroupBroadcast(A.w, 5);
  C.y += B2.x * subgroupBroadcast(A.x, 6);
  C.y += B2.y * subgroupBroadcast(A.y, 6);
  C.y += B2.z * subgroupBroadcast(A.z, 6);
  C.y += B2.w * subgroupBroadcast(A.w, 6);
  C.y += B3.x * subgroupBroadcast(A.x, 7);
  C.y += B3.y * subgroupBroadcast(A.y, 7);
  C.y += B3.z * subgroupBroadcast(A.z, 7);
  C.y += B3.w * subgroupBroadcast(A.w, 7);

  C.z += B0.x * subgroupBroadcast(A.x, 8);
  C.z += B0.y * subgroupBroadcast(A.y, 8);
  C.z += B0.z * subgroupBroadcast(A.z, 8);
  C.z += B0.w * subgroupBroadcast(A.w, 8);
  C.z += B1.x * subgroupBroadcast(A.x, 9);
  C.z += B1.y * subgroupBroadcast(A.y, 9);
  C.z += B1.z * subgroupBroadcast(A.z, 9);
  C.z += B1.w * subgroupBroadcast(A.w, 9);
  C.z += B2.x * subgroupBroadcast(A.x, 10);
  C.z += B2.y * subgroupBroadcast(A.y, 10);
  C.z += B2.z * subgroupBroadcast(A.z, 10);
  C.z += B2.w * subgroupBroadcast(A.w, 10);
  C.z += B3.x * subgroupBroadcast(A.x, 11);
  C.z += B3.y * subgroupBroadcast(A.y, 11);
  C.z += B3.z * subgroupBroadcast(A.z, 11);
  C.z += B3.w * subgroupBroadcast(A.w, 11);

  C.w += B0.x * subgroupBroadcast(A.x, 12);
  C.w += B0.y * subgroupBroadcast(A.y, 12);
  C.w += B0.z * subgroupBroadcast(A.z, 12);
  C.w += B0.w * subgroupBroadcast(A.w, 12);
  C.w += B1.x * subgroupBroadcast(A.x, 13);
  C.w += B1.y * subgroupBroadcast(A.y, 13);
  C.w += B1.z * subgroupBroadcast(A.z, 13);
  C.w += B1.w * subgroupBroadcast(A.w, 13);
  C.w += B2.x * subgroupBroadcast(A.x, 14);
  C.w += B2.y * subgroupBroadcast(A.y, 14);
  C.w += B2.z * subgroupBroadcast(A.z, 14);
  C.w += B2.w * subgroupBroadcast(A.w, 14);
  C.w += B3.x * subgroupBroadcast(A.x, 15);
  C.w += B3.y * subgroupBroadcast(A.y, 15);
  C.w += B3.z * subgroupBroadcast(A.z, 15);
  C.w += B3.w * subgroupBroadcast(A.w, 15);
}

void main()
{
    uvec2 tileID = uvec2(gl_WorkGroupID.xy);

    // Initialize result to zero
    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            C[i][j] = vec4(0.f, 0.f, 0.f, 0.f);
        }
    }
    uint s = gl_SubgroupSize;
    uint laneId = gl_LocalInvocationID.x;
    // On each iteration, load a row of cooperative matrices from matrix A,
    // load a column of cooperative matrices from matrix B, and multiply all
    // pairs of those matrices.
    for (uint chunkK = 0; chunkK < K; chunkK += lK) {
        vec4 matA[C_ROWS];
        [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
            uint gi = TILE_M * tileID.y + lM * i +(laneId*4) /16;
            uint gk = chunkK + (laneId*4) % 16;
            matA[i] = inputA.x[coordToOffset(gi, gk, strideA)/4];
        }
        vec4 matB0;
        vec4 matB1;
        vec4 matB2;
        vec4 matB3;
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            uint gj = TILE_N * tileID.x + lN * j + laneId;
            uint gk = chunkK;
            matB0.x = inputB.x[coordToOffset(gk, gj, strideB)];
            matB0.y = inputB.x[coordToOffset(gk+1, gj, strideB)];
            matB0.z = inputB.x[coordToOffset(gk+2, gj, strideB)];
            matB0.w = inputB.x[coordToOffset(gk+3, gj, strideB)];
            matB1.x = inputB.x[coordToOffset(gk+4, gj, strideB)];
            matB1.y = inputB.x[coordToOffset(gk+5, gj, strideB)];
            matB1.z = inputB.x[coordToOffset(gk+6, gj, strideB)];
            matB1.w = inputB.x[coordToOffset(gk+7, gj, strideB)];
            matB2.x = inputB.x[coordToOffset(gk+8, gj, strideB)];
            matB2.y = inputB.x[coordToOffset(gk+9, gj, strideB)];
            matB2.z = inputB.x[coordToOffset(gk+10, gj, strideB)];
            matB2.w = inputB.x[coordToOffset(gk+11, gj, strideB)];
            matB3.x = inputB.x[coordToOffset(gk+12, gj, strideB)];
            matB3.y = inputB.x[coordToOffset(gk+13, gj, strideB)];
            matB3.z = inputB.x[coordToOffset(gk+14, gj, strideB)];
            matB3.w = inputB.x[coordToOffset(gk+15, gj, strideB)];

            [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
              matmul4x16x16(C[i][j], matA[i], matB0, matB1, matB2, matB3);
            }
        }
    }
        
    // Initialize result to zero
    [[unroll]] for (uint i = 0; i < C_ROWS; ++i) {
        [[unroll]] for (uint j = 0; j < C_COLS; ++j) {
            uint gi = TILE_M * tileID.y + lM * i;
            uint gj = TILE_N * tileID.x + lN * j + laneId;
            outputO.x[coordToOffset(gi, gj, strideD)] = C[i][j].x;
            outputO.x[coordToOffset(gi+1, gj, strideD)] = C[i][j].y;
            outputO.x[coordToOffset(gi+2, gj, strideD)] = C[i][j].z;
            outputO.x[coordToOffset(gi+3, gj, strideD)] = C[i][j].w;
        }
    }
}
